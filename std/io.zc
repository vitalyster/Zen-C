
import "./core.zc"
import "./string.zc"

raw {
    int _z_vprintf(char* fmt, va_list ap) {
        return vprintf((const char*)fmt, ap);
    }
    
    int _z_vsnprintf(char* str, size_t size, char* fmt, va_list ap) {
        return vsnprintf(str, size, (const char*)fmt, ap);
    }

    void* _z_get_stdin() { return stdin; }
    int _z_get_eof() { return EOF; }
    int _z_fgetc(void* stream) { return fgetc((FILE*)stream); }
}

extern fn _z_vprintf(fmt: char*, ap: va_list) -> int;
extern fn _z_vsnprintf(str: char*, size: usize, fmt: char*, ap: va_list) -> int;

fn format(fmt: char*, ...) -> char* {
    static var buffer: char[1024];
    var ap: va_list;
    va_start(ap, fmt);
    _z_vsnprintf(buffer, 1024, fmt, ap);
    va_end(ap);
    return (char*)buffer;
}

fn format_into(buffer: char*, size: usize, fmt: char*, ...) -> int {
    var ap: va_list;
    va_start(ap, fmt);
    var ret = _z_vsnprintf(buffer, size, fmt, ap);
    va_end(ap);
    return ret;
}

fn format_new(fmt: char*, ...) -> char* {
    var buffer: char* = malloc(1024);
    if buffer == NULL return NULL;
    
    var ap: va_list;
    va_start(ap, fmt);
    _z_vsnprintf(buffer, 1024, fmt, ap);
    va_end(ap);
    return buffer;
}

fn print(fmt: char*, ...) -> int {
    var ap: va_list;
    va_start(ap, fmt);
    var ret = _z_vprintf(fmt, ap);
    va_end(ap);
    return ret;
}

fn println(fmt: char*, ...) -> int {
    var ap: va_list;
    va_start(ap, fmt);
    var ret = _z_vprintf(fmt, ap);
    va_end(ap);
    puts("");
    return ret + 1;
}

fn readln() -> char* {
    var cap: usize = 64;
    var len: usize = 0;
    var line: char* = malloc(cap);
    if (line == NULL) return NULL;
    
    var c: int;
    var std_in = _z_get_stdin();
    var eof_val = _z_get_eof();

    while (true) {
        c = _z_fgetc(std_in);
        if (c == eof_val) break;
        if (c == 10) break; // '\n'

        if (len + 1 >= cap) {
            cap = cap * 2;
            var n = realloc(line, cap);
            if (n == NULL) {
                free(line);
                return NULL;
            }
            line = n;
        }
        
        line[len] = c;
        len = len + 1;
    }
    
    if (len == 0 && c == eof_val) {
        free(line);
        return NULL;
    }
    
    line[len] = 0;
    return line;
}
